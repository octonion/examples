!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! This program compares the performances of the matrix multiplication
! implemented in terms of i) a triple do-loop; ii) a fortran intrinsic
! function - matmul(a,b); and iii) a subroutine in INTEL MKL - ?GEMM.
program test
  implicit none
  ! The order of the square matrices is 2000.
  integer(kind=4)::n=2000
  ! Calculate the matrix multiplications:
  ! i) c:=a*b in a triple do-loop.
  ! ii) d:=a*b by matmul(a,b).
  ! iii) e:=a*b by dgemm in INTEL MKL.
  real(kind=8),allocatable::a(:,:),b(:,:),c(:,:),d(:,:),e(:,:)
  real(kind=8)::alpha,beta
  integer(kind=4)::i,j,k,lda,ldb,lde
  real(kind=8)::start,finish

  allocate(a(n,n),b(n,n),c(n,n),d(n,n),e(n,n))
  alpha=1.0;beta=1.0
  lda=n;ldb=n;lde=n

  ! Generate the matrices, a and b, randomly.
  call cpu_time(start)
  call random_seed()
  do j=1, n
     do i=1, n
        call random_number(a(i,j))
        call random_number(b(i,j))
     end do
  end do
  call cpu_time(finish)
  write(unit=6,fmt=100) "The generation of two matrices takes ",finish-start," seconds."

  ! i) c:=a*b in a triple do-loop.
  call cpu_time(start)
  c=0.0D0
  do j=1, n
     do i=1, n
        do k=1, n
           c(i,j)=c(i,j)+a(i,k)*b(k,j)
        enddo
     enddo
  enddo
  call cpu_time(finish)
  write(unit=6,fmt=100) "A triple do-loop takes ",finish-start," seconds."
  print *,c(1000,1000)

  ! ii) d:=a*b by matmul(a,b).
  call cpu_time(start)
  d=0.0D0
  d=matmul(a,b)
  call cpu_time(finish)
  write(unit=6,fmt=100) "A matmul(a,b) function takes ",finish-start," seconds."
  print *,d(1000,1000)

  ! iii) e:=a*b by dgemm in INTEL MKL.
  call cpu_time(start)
  e=0.0D0
  call dgemm("N","N",n,n,n,alpha,a,lda,b,ldb,beta,e,lde)
  call cpu_time(finish)
  write(unit=6,fmt=100) "A DGEMM subroutine takes ",finish-start," seconds."
  print *,e(1000,1000)

  deallocate(a,b,c,d,e)

  stop
100 format(A,F8.3,A)
end program test
